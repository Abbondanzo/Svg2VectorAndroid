group 'geekyappz'
version '1.0.2'

apply plugin: 'java'

repositories {
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(JavaVersion.VERSION_11.majorVersion))
    }
}

tasks.compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

//create a single Jar with all dependencies
tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Implementation-Title': 'Svg2VectorAndroid',
                'Implementation-Version': version,
                'Main-Class': 'com.vector.svg2vectorandroid.Runner'
    }
    archiveBaseName = project.name
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    doFirst {
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
    destinationDirectory = project.file("bin")
}

dependencies {
    implementation 'com.android.tools:sdk-common:31.5.0'
    implementation 'com.android.tools:common:31.5.0'
    testImplementation 'junit:junit:4.13.1'
}
